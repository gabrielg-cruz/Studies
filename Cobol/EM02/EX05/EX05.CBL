       IDENTIFICATION DIVISION.
       PROGRAM-ID. EX05.
       AUTHOR. LUIZ HENRIQUE DE SANTANA / GABRIEL GOMES DA CRUZ.
       INSTALLATION. FATEC-SP.
       DATE-WRITTEN. 12/10/2023.
       DATE-COMPILED. 
       SECURITY. APENAS O AUTOR PODE MODIFICA-LO.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
           SOURCE-COMPUTER.    IBM-PC.
           OBJECT-COMPUTER.    IBM-PC.
           SPECIAL-NAMES.      DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT ARQALU ASSIGN TO DISK ORGANIZATION 
               IS LINE SEQUENTIAL.
           SELECT ARQREP ASSIGN TO DISK ORGANIZATION 
               IS LINE SEQUENTIAL.
           SELECT RELAPROV ASSIGN TO DISK.

       DATA DIVISION.
       FILE SECTION.
       FD ARQALU LABEL RECORD ARE STANDARD
           VALUE OF FILE-ID IS "ARQALU.DAT".
           01 REG-MATRICULA-ALU.
             02 MATRICULA-ALU  PIC 9(07).
             02 MATRICULA-ALU-RED REDEFINES MATRICULA-ALU.
               03 MAT-P1       PIC 9(06).
               03 MAT-P2       PIC 9(01).
             02 NOME-ALU         PIC X(30).
             02 NOTA1-ALU        PIC 9(02)V9(02).
             02 NOTA2-ALU        PIC 9(02)V9(02).
             02 NOTA3-ALU        PIC 9(02)V9(02).
             02 FALT-ALU         PIC 9(02).
             02 SEX-ALU          PIC X(01).

       FD ARQREP LABEL RECORD ARE STANDARD
           VALUE OF FILE-ID IS "ARQREP.DAT".
           01 REG-REP.
             02 MATRICULA-REP    PIC 9(07).
             02 NOME-REP         PIC X(30).
             02 NOTA1-REP        PIC 9(02)V9(02).
             02 NOTA2-REP        PIC 9(02)V9(02).
             02 NOTA3-REP        PIC 9(02)V9(02).
             02 FALT-REP         PIC 9(02).
             02 SEX-REP          PIC X(01).

       FD RELAPROV LABEL RECORD IS OMITTED.
           01 REG-ATR              PIC X(80).

       WORKING-STORAGE SECTION.
           77 FIM-ARQ PIC X(03) VALUE "NAO".
           77 CT-LIN PIC 99 VALUE 26.
           77 CT-PAG PIC 99 VALUE ZEROS.
           77 WSOMA PIC 9(02)V9(02) VALUE ZEROS.
           77 WMEDIA PIC 9(02)V9(02) VALUE ZEROS.
           77 WSOMA-MEDIAS PIC 9(05)V9(02) VALUE ZEROS.
           77 CT-ALUNOS-APROVADOS PIC 9(05) VALUE ZEROS.
           77 WMEDIA-TURMA PIC 9(02)V9(02) VALUE ZEROS.

       01 CAB-01.
           02 FILLER PIC X(20) VALUE SPACES.
           02 FILLER PIC X(19) VALUE "RELAÇÃO DE ALUNOS".
           02 FILLER PIC X(01) VALUE SPACE.
           02 FILLER PIC X(09) VALUE "APROVADOS".
           02 FILLER PIC X(14) VALUE SPACES.
           02 FILLER PIC X(04) VALUE "PAG ".
           02 VAR-PAG PIC 9(03).
           02 FILLER PIC X(10) VALUE SPACES.

       01 CAB-02.
           02 FILLER PIC X(08) VALUE SPACES.
           02 FILLER PIC X(07) VALUE "NÚMERO".
           02 FILLER PIC X(16) VALUE SPACES.
           02 FILLER PIC X(13) VALUE "NOME DO ALUNO".
           02 FILLER PIC X(14) VALUE SPACES.
           02 FILLER PIC X(05) VALUE "MEDIA".
           02 FILLER PIC X(08) VALUE SPACES.
           02 FILLER PIC X(06) VALUE "FALTAS".
           02 FILLER PIC X(03) VALUE SPACES.

       01 DETALHE.
           02 FILLER PIC X(08) VALUE SPACES.
           02 DET-MATRICULA-P1 PIC 9(06).
           02 FILLER PIC X(01) VALUE "-".
           02 DET-MATRICULA-P2 PIC 9(01).
           02 FILLER PIC X(06) VALUE SPACES.
           02 DET-NOME PIC X(30).
           02 FILLER PIC X(05) VALUE SPACES.
           02 DET-MEDIA PIC Z9,99.
           02 FILLER PIC X(07) VALUE SPACES.
           02 DET-FALTAS PIC 99.
           02 FILLER PIC X(08) VALUE SPACES.

       01 TOTAL.
           02 FILLER PIC X(08) VALUE SPACES.
           02 FILLER PIC X(28) VALUE "Total de alunos aprovados: ".
           02 TOT-ALUNOS-APROVADOS PIC 9(05).
           02 FILLER PIC X(39) VALUE SPACES.

       01 GERAL.
           02 FILLER PIC X(08) VALUE SPACES.
           02 FILLER PIC X(27) VALUE "Media geral da turma     :".
           02 GER-MEDIA-GERAL PIC Z9,99.
           02 FILLER PIC X(40) VALUE SPACES.

       PROCEDURE DIVISION.
       PGM-EX05.
           PERFORM INICIO.
           PERFORM PRINCIPAL UNTIL FIM-ARQ EQUAL "SIM".
           PERFORM TERMINO.
           STOP RUN.

       INICIO.
           OPEN INPUT ARQALU OUTPUT ARQREP OUTPUT RELAPROV.
           PERFORM LEITURA.

       LEITURA.
           READ ARQALU AT END MOVE "SIM" TO FIM-ARQ.

       PRINCIPAL.
           PERFORM CALCMEDIA.
           PERFORM IMPRESSAO.
           PERFORM LEITURA.

       CALCMEDIA.
           ADD NOTA1-ALU NOTA2-ALU NOTA3-ALU GIVING WSOMA.
           DIVIDE WSOMA BY 3 GIVING WMEDIA.

       IMPRESSAO.
           IF (WMEDIA NOT LESS THAN 7,00) AND 
              (FALT-ALU LESS THAN 16)
               IF CT-LIN GREATER THAN 25 
                   PERFORM CABECALHO
               END-IF
               PERFORM IMP-DET
           ELSE
               PERFORM GRAVACAO
           END-IF.

       CABECALHO.
           ADD 1 TO CT-PAG.
           MOVE 1 TO CT-LIN.
           MOVE CT-PAG TO VAR-PAG.
           MOVE SPACES TO REG-ATR.
           WRITE REG-ATR AFTER ADVANCING PAGE.
           WRITE REG-ATR FROM CAB-01 AFTER ADVANCING 1 LINE.
           WRITE REG-ATR FROM CAB-02 AFTER ADVANCING 2 LINES.
           MOVE SPACES TO REG-ATR.
           WRITE REG-ATR AFTER ADVANCING 1 LINE.

       IMP-DET.
           MOVE MAT-P1 TO DET-MATRICULA-P1.
           MOVE MAT-P2 TO DET-MATRICULA-P2.
           MOVE NOME-ALU TO DET-NOME.
           MOVE WMEDIA TO DET-MEDIA.
           MOVE FALT-ALU TO DET-FALTAS.
           WRITE REG-ATR FROM DETALHE AFTER ADVANCING 1 LINE.
           ADD 1 TO CT-LIN.
           ADD 1 TO CT-ALUNOS-APROVADOS.
           ADD WMEDIA TO WSOMA-MEDIAS.

       GRAVACAO.
           MOVE MATRICULA-ALU TO MATRICULA-REP.
           MOVE NOME-ALU TO NOME-REP.
           MOVE NOTA1-ALU TO NOTA1-REP.
           MOVE NOTA2-ALU TO NOTA2-REP.
           MOVE NOTA3-ALU TO NOTA3-REP.
           MOVE FALT-ALU TO FALT-REP.
           MOVE SEX-ALU TO SEX-REP.
           WRITE REG-REP.

       RODAPE.
           DIVIDE WSOMA-MEDIAS BY CT-ALUNOS-APROVADOS 
               GIVING WMEDIA-TURMA.
           MOVE WMEDIA-TURMA TO GER-MEDIA-GERAL.
           MOVE CT-ALUNOS-APROVADOS TO TOT-ALUNOS-APROVADOS.
           WRITE REG-ATR FROM TOTAL AFTER ADVANCING 4 LINES.
           WRITE REG-ATR FROM GERAL AFTER ADVANCING 1 LINES.
           
       TERMINO.
           PERFORM RODAPE.
           CLOSE ARQALU ARQREP RELAPROV.