       IDENTIFICATION DIVISION.
       PROGRAM-ID. EX01.
       AUTHOR.  LUIZ HENRIQUE DE SANTANA / GABRIEL GOMES DA CRUZ.
       INSTALLATION. FATEC-SP.
       DATE-WRITTEN. 09/10/2023.
       DATE-COMPILED.
       SECURITY. APENAS O AUTOR PODE MODIFICAR.
      *REMARKS.  LER ARQUIVO DE ENTRADA CADSOC1 E SEPARAR OS DADOS DOS
      *          SOCIOS EM ATRASADOS (RELSOC) E NORMAIS (CADSOC2).

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
           OBJECT-COMPUTER.  IBM-PC.
           SOURCE-COMPUTER.  IBM-PC.
           SPECIAL-NAMES.    DECIMAL-POINT IS COMMA.
       
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CADSOC1 ASSIGN TO DISK ORGANIZATION 
             IS LINE SEQUENTIAL.
           SELECT CADSOC2 ASSIGN TO DISK ORGANIZATION 
             IS LINE SEQUENTIAL.
           SELECT RELSOCIO ASSIGN TO DISK.
          
       DATA DIVISION.
       FILE SECTION.
       FD CADSOC1 LABEL RECORD ARE STANDARD VALUE OF FILE-ID
           IS "CADSOC1.DAT".

           01 REG-SOC1-ENT.
             02 COD-PAG1      PIC 9(02).
               88 COD-VALIDO VALUE 1 THRU 2.
             02 NUM-SOC1      PIC 9(06).
             02 NOME-SOC1     PIC X(30).
             02 VAL-SOC1      PIC 9(09)V99.

       FD CADSOC2 LABEL RECORD ARE STANDARD VALUE OF FILE-ID
           IS "CADSOC2.DAT".

           01 REG-SOC2-SAI.
             02 NUM-SOC2      PIC 9(06).
             02 NOME-SOC2     PIC X(30).
             02 VAL-SOC2      PIC 9(09)V99.

       FD RELSOCIO LABEL RECORD IS OMITTED.
           01 REL-SOCIO PIC X(80).

       WORKING-STORAGE SECTION.
           77 FIM-ARQ PIC X(03) VALUE "NAO".
           77 CT-PAG PIC 9(02) VALUE ZEROS.
           77 CT-LIN PIC 9(02) VALUE 31.
           77 CT-SOC-ATR PIC 9(05) VALUE ZEROES.
           77 VAL-SOC-ATR PIC 9(12)V99 VALUE ZEROS.

           01 CAB-01.
             02 FILLER PIC X(70) VALUE SPACES.
             02 FILLER PIC X(05) VALUE "PAG. ".
             02 VAR-PAG PIC 99.
             02 FILLER PIC X(03) VALUE SPACES.

           01 CAB-02.
             02 FILLER PIC X(27) VALUE SPACES.
             02 FILLER PIC X(27) VALUE "RELACAO DE SOCIOS ATRASADOS".
             02 FILLER PIC X(26) VALUE SPACES.

           01 CAB-03.
             02 FILLER PIC X(04) VALUE SPACES.
             02 FILLER PIC X(15) VALUE "NUMERO DO SOCIO".
             02 FILLER PIC X(13) VALUE SPACES.
             02 FILLER PIC X(13) VALUE "NOME DO SOCIO".
             02 FILLER PIC X(13) VALUE SPACES.
             02 FILLER PIC X(18) VALUE "VALOR DO PAGAMENTO".
             02 FILLER PIC X(04) VALUE SPACES.

           01 ROD-01.
             02 FILLER PIC X(04) VALUE SPACES.
             02 FILLER PIC X(27) VALUE "Total de Socios atrasados: ".
             02 VAR-SOC-ATR PIC 9(05).
             02 FILLER PIC X(44) VALUE SPACES.

           01 ROD-02.
             02 FILLER PIC X(04) VALUE SPACES.
             02 FILLER PIC X(27) VALUE "Valor Total atrasado     : ".
             02 VAR-VAL-SOC PIC ZZZ.ZZZ.ZZZ.ZZ9,99.
             02 FILLER PIC X(31) VALUE SPACES.

           01 DETALHE.
             02 FILLER PIC X(08) VALUE SPACES.
             02 DET-NUM PIC 999.999.
             02 FILLER PIC X(08) VALUE SPACES.
             02 DET-NOME PIC X(30).
             02 FILLER PIC X(06) VALUE SPACES.
             02 DET-VAL PIC ZZZ.ZZZ.ZZ9,99.
             02 FILLER PIC X(07) VALUE SPACES.

       PROCEDURE DIVISION.
       PGM-EX01.
           PERFORM INICIO.
           PERFORM PRINCIPAL UNTIL FIM-ARQ EQUAL "SIM".
           PERFORM TERMINO.
           STOP RUN.
       
       INICIO.
           OPEN INPUT CADSOC1 OUTPUT CADSOC2 RELSOCIO.
           PERFORM LEITURA.

       LEITURA.
           READ CADSOC1 AT END MOVE "SIM" TO FIM-ARQ.

       PRINCIPAL.
           PERFORM VALIDACAO.
           PERFORM LEITURA.
           IF FIM-ARQ EQUAL "SIM"
               PERFORM RODAPE-RELSOC.

       VALIDACAO.
           IF COD-VALIDO
               IF COD-PAG1 EQUAL 1
                   PERFORM GRAVA-CADSOC2
               ELSE
                   PERFORM GRAVA-RELSOC.

       GRAVA-CADSOC2.
           MOVE NUM-SOC1 TO NUM-SOC2.
           MOVE NOME-SOC1 TO NOME-SOC2.
           MOVE VAL-SOC1 TO VAL-SOC2.
           WRITE REG-SOC2-SAI.

       GRAVA-RELSOC.
           IF CT-LIN GREATER THAN 30
               PERFORM CABECALHO-RELSOC.
           PERFORM IMP-DET-RELSOC.

       CABECALHO-RELSOC.
           ADD 1 TO CT-PAG.
           MOVE CT-PAG TO VAR-PAG.
           MOVE SPACES TO REL-SOCIO.
           WRITE REL-SOCIO AFTER ADVANCING PAGE.
           WRITE REL-SOCIO FROM CAB-01 AFTER ADVANCING 2 LINE.
           WRITE REL-SOCIO FROM CAB-02 AFTER ADVANCING 3 LINES.
           WRITE REL-SOCIO FROM CAB-03 AFTER ADVANCING 3 LINES.
           MOVE SPACES TO REL-SOCIO.
           WRITE REL-SOCIO AFTER ADVANCING 1 LINE.
           MOVE 1 TO CT-LIN.

       IMP-DET-RELSOC.
           MOVE NUM-SOC1 TO DET-NUM.
           MOVE NOME-SOC1 TO DET-NOME.
           MOVE VAL-SOC1 TO DET-VAL.
           WRITE REL-SOCIO FROM DETALHE AFTER ADVANCING 1 LINE.
           ADD 1 TO CT-LIN.
           ADD 1 TO CT-SOC-ATR.
           ADD VAL-SOC1 TO VAL-SOC-ATR.

       RODAPE-RELSOC.
           MOVE CT-SOC-ATR TO VAR-SOC-ATR.
           MOVE VAL-SOC-ATR TO VAR-VAL-SOC.
           WRITE REL-SOCIO FROM ROD-01 AFTER ADVANCING 3 LINES.
           WRITE REL-SOCIO FROM ROD-02 AFTER ADVANCING 1 LINE.
           
       TERMINO.
           CLOSE CADSOC1 CADSOC2 RELSOCIO.